
options {
  JAVA_UNICODE_ESCAPE = true;
  STATIC = false;

  //MULTI = true;
  //NODE_DEFAULT_VOID = true;
  //NODE_SCOPE_HOOK = true;
  DEBUG_PARSER = false;

    //USER_CHAR_STREAM = true; // user provided CharStream - LexerInputCharStream
    //BUILD_TOKEN_MANAGER = true; // build token manager
    //UNICODE_INPUT = true; // input will be all unicode characters
}

PARSER_BEGIN(Parser)

package conversation.parser;
import conversation.core.*;
import conversation.core.conditions.*;
import conversation.core.effects.*;
import java.util.*;

public class Parser {

    /*void jjtreeOpenNodeScope(Node n) {
	((ParseNode)n).firstToken = getToken(1);
    }

    void jjtreeCloseNodeScope(Node n) {
	((ParseNode)n).lastToken = getToken(0);
    }*/
}

PARSER_END(Parser)



/* WHITE SPACE */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}





/* LITERALS */

TOKEN :
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'"
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}

/* KEYWORDS */

TOKEN :
{
    < CONVERSATION: "conversation" >
|   < PARTICIPANT: "participant" >
|   < STARTING: "starting" >
|   < TOPIC: "topic" >
|   < BEAT: "beat" >
|   < NODE: "node" >
|   < OP: "op" >
|   < SEQ: "seq" >
|   < BREAK: "break" >
|   < END: "end" >
|   < WHEN: "when" >
|   < SET: "set" >
|   < CLEAR: "clear" >
}


/* IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}

/* SEPARATORS */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < AT: "@" >
}

/* OPERATORS */

TOKEN :
{
  < ASSIGN: "=" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < XOR: "^" >
| < REM: "%" >
| < LSHIFT: "<<" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
| < LSHIFTASSIGN: "<<=" >
| < RSIGNEDSHIFTASSIGN: ">>=" >
| < RUNSIGNEDSHIFTASSIGN: ">>>=" >
| < ELLIPSIS: "..." >
}

/* >'s need special attention due to generics syntax. */
TOKEN :
{
  < RUNSIGNEDSHIFT: ">>>" >
  {
     matchedToken.kind = GT;
     ((Token.GTToken)matchedToken).realKind = RUNSIGNEDSHIFT;
     input_stream.backup(2);
  }
| < RSIGNEDSHIFT: ">>" >
  {
     matchedToken.kind = GT;
     ((Token.GTToken)matchedToken).realKind = RSIGNEDSHIFT;
     input_stream.backup(1);
  }
| < GT: ">" >
}



/****************************************
 *  The Conversation Language Grammar starts here  *
 ****************************************/



Conversation ConversationTopLevel()  : {
    Token t;
    DialogueAgent participant;
    Conversation c = new Conversation();
    Topic topic;
} {
    <CONVERSATION> t = <IDENTIFIER> {c.setLabel(t.image);} <SEMICOLON>
    (participant = Participant() {c.addAgent(participant);} )+

    ( topic = TopicNode() {c.getAllTopics().add(topic);} )*

    <EOF>
    {return c;}
}

DialogueAgent Participant()  : {
    Token t;
    Map<String, Object> attributes = null;
} {
    <PARTICIPANT> t = <IDENTIFIER> [<LBRACE>
        attributes = Attributes()
    <RBRACE>]
    {
        DialogueAgent agent = new DialogueAgent(t.image);
        if(attributes != null)
            agent.getAttributes().putAll(attributes);
        return agent;
    }
}

Topic TopicNode()  : {
    Topic.Builder builder = new Topic.Builder();
    Condition condition;
    Map<String, Object> attributes;
    DialogueBeat beat;
    Token t;
} {
    [<STARTING> {builder.starting = true;}] <TOPIC> [t = <IDENTIFIER> {builder.label = t.image;}] <LBRACE>
        [condition = When() {builder.condition = condition;}]
        attributes = Attributes() {builder.attributes = attributes;}
        ( beat = BeatNode() {builder.beats.add(beat);} )+
    <RBRACE>

    {return builder.build();}
}

DialogueBeat BeatNode()  : {
    DialogueBeat.Builder builder = new DialogueBeat.Builder();
    NodeGroup root;
    Condition condition;
    Map<String, Object> attributes;
    Token t;
} {
    [<STARTING> {builder.starting = true;}] <BEAT> [t = <IDENTIFIER> {builder.label = t.image;}] <LBRACE>
        [condition = When() {builder.condition = condition;}]
        attributes = Attributes() {builder.attributes = attributes;}
        root = GroupNode() {builder.root = root;}
    <RBRACE>
    {return builder.build();}
}

NodeGroup GroupNode()  : {
    DialogueNode leaf;
    NodeGroup group;
    NodeGroup.Builder builder = new NodeGroup.Builder();
    NodeLink link;
    Token t;
} {
    ( <SEQ> [t = <IDENTIFIER> {builder.label = t.image;}] {builder.type = NodeGroup.Type.sequential;} |
      <OP> [t = <IDENTIFIER> {builder.label = t.image;}] {builder.type = NodeGroup.Type.optional;} ) <LBRACE>
        ( leaf = LeafNode() {builder.nodes.add(leaf);}
        | group = GroupNode() {builder.nodes.add(group);}
        | link = LinkNode() {builder.nodes.add(link);} [<COMMA>]
        | <BEAT> <BREAK> {builder.nodes.add(conversation.core.Node.BEAT_BREAK);} [<COMMA>]
        | <TOPIC> <BREAK> {builder.nodes.add(conversation.core.Node.TOPIC_BREAK);} [<COMMA>]
        | <END> <CONVERSATION> {builder.nodes.add(conversation.core.Node.CONVERSATION_END);} [<COMMA>]
        )+
    <RBRACE>

    {return builder.build();}
}

NodeLink LinkNode() : {
    Token t;
} {
    <BIT_AND> t = <IDENTIFIER>
    {return new NodeLink(t.image);}
}

DialogueNode LeafNode()  : {
    Token t;
    DialogueNode.Builder builder = new DialogueNode.Builder();
    Condition condition;
    Map<String, Object> attributes;
    List<Effect> effects;
} {
    <NODE> [t = <IDENTIFIER> {builder.label = t.image;}] <LBRACE>
        [condition = When() {builder.condition = condition;}]
        attributes = Attributes() {builder.attributes = attributes;}
        [effects = Effects() {builder.effects = effects;}]
    <RBRACE>

    {return builder.build();}
}

List<Effect> Effects()  : {
    List<Effect> r = new ArrayList<Effect>();
    Effect effect;
} {
    <SET> (effect = Effect() {r.add(effect);} <SEMICOLON>)*
    {return r;}
}

Effect Effect()  : {
    Token t;
    Object value;
    Effect effect;
    SetFlagEffect.ClearState clearState = SetFlagEffect.ClearState.never;
} {
    t = <IDENTIFIER> (
          <ASSIGN> value = Value() [<CLEAR> ( <BEAT> {clearState = SetFlagEffect.ClearState.beat;} | <TOPIC> {clearState = SetFlagEffect.ClearState.topic;} )]
            {effect = new SetFlagEffect(t.image, value, clearState);}
        | <INCR> {effect = new IncrementFlagEffect(t.image);}
        | <DECR> {effect = new DecrementFlagEffect(t.image);}
    )
    {return effect;}
}

// literal or identifier
Object Value()  : {
    Token value;
    Object result;
} {
    (     value = <FLOATING_POINT_LITERAL> {result = Double.valueOf(value.image);}
        | value = <INTEGER_LITERAL> {result = Integer.valueOf(value.image);}
        | value = <STRING_LITERAL> {result = value.image.substring(1,value.image.length()-1);}
        | value = <IDENTIFIER> {result = value.image;}
    )
    {return result;}
}

Map<String, Object> Attributes()  : {
    Token key;
    Object value;
    Map<String, Object> r = new HashMap<String, Object>();
} {
    ( key = <IDENTIFIER> <COLON> value = Value() {r.put(key.image, value);} [<COMMA>] )*
    {return r;}
}

Condition When()  : {
    Condition result;
} {
    <WHEN> result = Condition()
    {return result;}
}

Condition Condition()  : {
    Condition r;
    Token t;
} {
    ( <LPAREN> r = Condition1() <RPAREN>
    | <BANG> r = Condition() {r = new Not(r);}
    | t = <IDENTIFIER> {r = new LogicVariable(t.image);}
//    | r = TestCondition()
    )
    {return r;}
}

// for and, or blocks.
Condition Condition1()  : {
    Condition r;
    List<Condition> body = new ArrayList<Condition>(); // body for and/or blocks.
} {
    r = Condition() {body.add(r);}

        ( (<SC_AND> r = Condition() {body.add(r);})+ {r = new MultiAnd(body);} )
    |   ( (<SC_OR> r = Condition() {body.add(r);})+ {r = new MultiOr(body);} )

    {return r;}
}

/*
In terms of getting test conditions to work: probably want to refactor stuff in
conversation.core.conditions to have a Test class, which has inside of it each of the
test types (>, >=, <, <=, ==, etc), removing the individual classes.
Secondly, also have a SymbolOperation class? with Arithmetic things (+,-,/,*, etc)

Condition TestCondition() : {
    SymbolExpression ex1, ex2;

} {
    ex1 = SymbolExpression()
    (<>)
}
*/